apply plugin: 'com.android.library'

def versionMajor = 0
def versionMinor = 2
def versionPatch = 3
def versionBuild = 0

def libVersionCode = versionMajor * 1000 + versionMinor * 100 + versionPatch * 10 + versionBuild
def libVersionName = "${versionMajor}.${versionMinor}.${versionPatch}"

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 24
        versionCode libVersionCode
        versionName libVersionName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    packagingOptions {
        exclude 'asm-license.txt'
        exclude 'LICENSE'
        exclude 'LICENSE.txt'
        exclude 'NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE'
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

group = 'xyz.dokup.multiselectablecalendar'
version = libVersionName

def siteUrl = 'https://github.com/e10dokup/MultiSelecableCalendar'
def gitUrl = 'https://github.com/e10dokup/MultiSelecableCalendar.git'

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_API_KEY')

    publish = true
    configurations = ['archives']
    pkg {
        repo = 'maven'
        name = project.name
        userOrg = 'e10dokup'
        desc = 'Multi state holding calendar'
        websiteUrl = siteUrl
        issueTrackerUrl = "${siteUrl}/issues"
        vcsUrl = gitUrl
        licenses = ['Apache-2.0']
        labels = ['android']
        publicDownloadNumbers = true
        version {
            name = libVersionName
            desc = "MultiSelectableCalendar ${libVersionName}"
            released  = new Date()
            vcsTag = libVersionName
        }
    }
}

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'
                name 'multiselectablecalendar'
                url siteUrl
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'e10dokup'
                        name 'Yoshihiro Wada'
                        email 'wada@dokup.xyz'
                    }
                }
                scm {
                    connection 'https://github.com/e10dokup/MultiSelecableCalendar.git'
                    developerConnection 'https://github.com/e10dokup/MultiSelecableCalendar.git'
                    url siteUrl
                }
            }
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:24.1.1'

    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test:rules:0.5'
    androidTestCompile 'com.squareup.assertj:assertj-android:1.1.1'

}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}

task findConventions << {
    println project.getConvention()
}
